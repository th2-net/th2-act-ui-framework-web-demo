plugins {
    id 'com.palantir.docker' version '0.25.0' apply false
    id "com.jfrog.artifactory" version '4.28.2'
}

apply plugin: 'application'
apply plugin: 'com.palantir.docker'
apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'com.jfrog.artifactory'

ext {
    sharedDir           = file("${project.rootDir}/shared")
}

group = 'com.exactpro.th2'
version = release_version

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    maven {
        name 'MavenLocal'
        url sharedDir
    }
    mavenCentral()
    maven {
        name 'Sonatype_snapshots'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
    }
    mavenLocal()

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

artifactoryPublish.skip = true

dependencies {
    api platform('com.exactpro.th2:bom:3.2.0')
    implementation "com.exactpro.th2:common:3.39.1"
    implementation "com.exactpro.th2:grpc-common:3.11.1"
    implementation "com.exactpro.th2:grpc-act-uiframework-web-demo:3.2.0-TH2-3318-rest-2551661650-SNAPSHOT"
    implementation "com.exactpro.th2:grpc-check1:3.6.0"
    implementation "com.exactpro.th2:grpc-hand:2.10.0"
    implementation "com.exactpro.th2:act-gui-core:2.5.0"

    implementation 'org.apache.httpcomponents:httpclient:4.5.13'

    implementation "com.fasterxml.jackson.core:jackson-core:2.13.3"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.13.3"

    implementation "org.slf4j:slf4j-log4j12:1.7.36"
    implementation "org.slf4j:slf4j-api:1.7.36"
}

application {
    mainClassName 'com.exactpro.th2.act.ActMain'
}

applicationName = 'service'

distTar {
    archiveName "${applicationName}.tar"
}

dockerPrepare {
    dependsOn distTar
}

jar {
    manifest {
        attributes(
                'Created-By':               "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
                'Specification-Title':      '',
                'Specification-Vendor':     'Exactpro Systems LLC',
                'Implementation-Title':     project.archivesBaseName,
                'Implementation-Vendor':    'Exactpro Systems LLC',
                'Implementation-Vendor-Id': 'com.exactpro',
                'Implementation-Version':   project.version
        )
    }
}

artifactory {
    publish {
        defaults {
            // Reference to Gradle publications defined in the build script.
            // This is how we tell the Artifactory Plugin which artifacts should be
            // published to Artifactory.
            publications('mavenJava')
            publishArtifacts = true
            publishBuildInfo = true
            // Properties to be attached to the published artifacts.
            publishPom = true
        }
    }
}

docker {
    copySpec.from(tarTree("$buildDir/distributions/${applicationName}.tar"))
}
